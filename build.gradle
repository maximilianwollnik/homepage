/*
 * Copyright (c) 2021, Maximilian Wollnik, All rights reserved.
 */

plugins {
    id 'org.springframework.boot' version '2.5.4'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
}

group = 'de.maximilianwollnik'
sourceCompatibility = '16'

repositories {
    mavenCentral()
}

ext {
    set('springCloudVersion', "2020.0.3")
    set('asciidoctorjVersion', "2.5.2")
    set('asciidoctorjPdfVersion', "1.6.0")
    set('commonsioVersion', "2.11.0")
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
    implementation 'org.springframework.cloud:spring-cloud-starter-bootstrap'
    implementation 'org.springframework.cloud:spring-cloud-starter-config'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation "commons-io:commons-io:${commonsioVersion}"

    implementation "org.asciidoctor:asciidoctorj:${asciidoctorjVersion}"
    implementation "org.asciidoctor:asciidoctorj-pdf:${asciidoctorjPdfVersion}"

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
}

test {
    useJUnitPlatform()
}

task('installWebapp', group: 'build setup') {
    doLast {
        exec {
            workingDir 'src/main/frontend'
            if (System.getProperty('os.name').toLowerCase(Locale.ROOT).contains('windows')) {
                commandLine 'cmd', '/c', 'npm', 'install'
            } else {
                commandLine 'npm', 'install'
            }
        }
    }
}

task('buildWebapp', group: 'build') {
    doLast {
        exec {
            workingDir 'src/main/frontend'
            if (System.getProperty('os.name').toLowerCase(Locale.ROOT).contains('windows')) {
                commandLine 'cmd', '/c', 'npm', 'run', 'build'
            } else {
                commandLine 'npm', 'run', 'build'
            }
        }
    }
}

springBoot {
    buildInfo()
}

bootJar.dependsOn buildWebapp
bootRun.dependsOn buildWebapp
